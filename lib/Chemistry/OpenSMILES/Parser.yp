# Header section

%{

use warnings;
use 5.0100;

use Chemistry::OpenSMILES qw(is_aromatic);
use Graph::Undirected;
use List::Util qw(sum);

my %normal_valence = (
    B  => [ 3 ],
    C  => [ 4 ],
    N  => [ 3, 5 ],
    O  => [ 2 ],
    P  => [ 3, 5 ],
    S  => [ 2, 4, 6 ],
    F  => [ 1 ],
    Cl => [ 1 ],
    Br => [ 1 ],
    I  => [ 1 ],

    c  => [ 3 ],
);

my %bond_order = (
    '-' => 1,
    '=' => 2,
    '#' => 3,
    '$' => 4,
);

%}

%%

# Rules section

# The top-level 'filter' rule

smiles: chain ;

chain: atom
        {
            my $g = Graph::Undirected->new( multiedged =>
                                                $_[0]->{USER}{OPTIONS}{polymer} );
            $g->add_vertex( $_[1] );
            push @{$_[0]->{USER}{GRAPHS}}, $g;

            $_[1]->{graph} = $g;
            $_[1]->{index} = @{$_[0]->{USER}{GRAPHS}}-1;

            return { first => $_[1],
                     last  => $_[1] };
        }
     | chain atom
        {
            $_[2]->{graph} = $_[1]->{last}{graph};
            $_[2]->{index} = $_[1]->{last}{index};

            _add_bond( $_[2]->{graph},
                       $_[1]->{last},
                       $_[2],
                       is_aromatic $_[1]->{last} && is_aromatic $_[1]->{last} ? ':' : undef );
            $_[1]->{last} = $_[2];

            return $_[1];
        }
     | chain bond atom
        {
            $_[3]->{graph} = $_[1]->{last}{graph};
            $_[3]->{index} = $_[1]->{last}{index};

            _add_bond( $_[3]->{graph},
                       $_[1]->{last},
                       $_[3],
                       $_[2] ne '-' ? $_[2] : undef );
            $_[1]->{last} = $_[3];

            return $_[1];
        }
     | chain '.' atom
        {
            my $g = Graph::Undirected->new( multiedged =>
                                                $_[0]->{USER}{OPTIONS}{polymer} );
            $g->add_vertex( $_[3] );
            push @{$_[0]->{USER}{GRAPHS}}, $g;

            $_[3]->{graph} = $g;
            $_[3]->{index} = @{$_[0]->{USER}{GRAPHS}}-1;

            return { first => $_[3],
                     last  => $_[3] };
        }
     | chain '(' chain ')'
        {
            if( $_[1]->{last}{index} != $_[3]->{first}{index} ) {
                $_[0]->_merge_graphs( $_[1]->{last}{index},
                                      $_[3]->{first}{index} );
            }

            _add_bond( $_[1]->{last}{graph},
                       $_[1]->{last},
                       $_[3]->{first},
                       is_aromatic $_[1]->{last} && is_aromatic $_[3]->{first} ? ':' : undef );

            return $_[1];
        }
     | chain '(' bond chain ')'
        {
            if( $_[1]->{last}{index} != $_[4]->{first}{index} ) {
                $_[0]->_merge_graphs( $_[1]->{last}{index},
                                      $_[4]->{first}{index} );
            }

            _add_bond( $_[1]->{last}{graph},
                       $_[1]->{last},
                       $_[4]->{first},
                       $_[3] ne '-' ? $_[3] : undef );

            return $_[1];
        }
     | chain '(' '.' chain ')'

     # According to the specification of OpenSMILES, ring bonds are
     # allowed only before the branch enumeration. However, I think this
     # is too strict.

     | chain ringbond
        {
            $_[0]->_add_ring_bond( $_[1]->{last}, $_[2] );
            return $_[1];
        }
     | chain bond ringbond
        {
            $_[0]->_add_ring_bond( $_[1]->{last}, $_[3], $_[2] );
            return $_[1];
        }

     # Polymer bonds. Implementation slightly differs from the OpenSMILES
     # specification, as the specification is not quite clear there.

     | chain polybond
        {
            $_[0]->_add_poly_bond( $_[1]->{last}, $_[2] );
            return $_[1];
        }
     | chain bond polybond
        {
            $_[0]->_add_poly_bond( $_[1]->{last}, $_[3], $_[2] );
            return $_[1];
        }
     ;

bond: '-' | '=' | '#' | '$' | ':' | '/' | '\\' ;

%%

# Footer section

sub _Error
{
    my( $self ) = @_;
    close $self->{USER}{FILEIN} if $self->{USER}{FILEIN};

    if( ${$self->{TOKEN}} eq '' &&
        grep { defined $_ && !ref $_ && $_ eq '(' }
        map { $_->[1] } @{$self->{STACK}} ) {
        die "$0: syntax error: missing closing parenthesis.\n";
    }

    if( ${$self->{TOKEN}} eq ')' ) {
        die "$0: syntax error: unbalanced parentheses.\n";
    }

    my $msg = "$0: syntax error at position $self->{USER}{CHARNO}";
    if( $self->YYData->{INPUT} ) {
        $self->YYData->{INPUT} =~ s/\n$//;
        die "$msg: '" . $self->YYData->{INPUT} . "'.\n";
    } else {
        die "$msg.\n";
    }
}

sub _Lexer
{
    my( $self ) = @_;

    # If the line is empty and the input is originating from the file,
    # another line is read.
    if( !$self->YYData->{INPUT} && $self->{USER}{FILEIN} ) {
        my $filein = $self->{USER}{FILEIN};
        $self->YYData->{INPUT} = <$filein>;
        $self->{USER}{CHARNO} = 0;
    }

    $self->YYData->{INPUT} =~ s/^(\s+)//;
    $self->{USER}{CHARNO} += length $1 if defined $1;

    # Bracket atoms
    if( $self->YYData->{INPUT} =~ s/^\[ (?<isotope>[0-9]+)?
                                        (?<symbol>[A-Za-z][a-z]?|\*)
                                        (?<chirality>@(
                                         (TH|AL)[12]       |
                                         SP     [123]      |
                                         (TB|OH)[0-9]{1,2} |
                                         @?
                                         ))?
                                        (?<hcount>H[0-9]?)?
                                        (?<charge>--|\+\+|[-+][0-9]{0,2})?
                                        (:(?<class>[0-9]+))? \]//x ) {
        my $atom = { %+, number => $self->{USER}{ATOMNO} };
        $self->{USER}{ATOMNO} ++;
        $self->{USER}{CHARNO} += length $&;

        if( $atom->{charge} ) {
            $atom->{charge} =~ s/^([-+])$/${1}1/;
            $atom->{charge} =~ s/^([-+])\1$/${1}2/;
            $atom->{charge} = int $atom->{charge};
        }

        if( $atom->{chirality} ) {
            $atom->{chirality} =~ s/^\@TH1$/@/;
            $atom->{chirality} =~ s/^\@TH2$/@@/;
        }

        if( $atom->{hcount} ) {
            $atom->{hcount} =~ s/^H//;
            $atom->{hcount} = $atom->{hcount} ? int $atom->{hcount} : 1;
        } else {
            $atom->{hcount} = 0;
        }

        if( $atom->{isotope} ) {
            $atom->{isotope} = int $atom->{isotope};
        }

        # Atom class is an arbitrary number, 0 by default
        $atom->{class} = exists $atom->{class} ? int $atom->{class} : 0;

        return ( 'atom', $atom );
    }

    # Bracketless atoms
    if( $self->YYData->{INPUT} =~ s/^(Br|Cl|[BCINOPSFbcnops*])// ) {
        my $atom = { symbol => $1,
                     class  => 0,
                     number => $self->{USER}{ATOMNO} };
        $self->{USER}{ATOMNO} ++;
        $self->{USER}{CHARNO} += length $&;
        return ( 'atom', $atom );
    }

    # Ring bonds
    if( $self->YYData->{INPUT} =~ s/^%([0-9]{2})// ||
        $self->YYData->{INPUT} =~ s/^([0-9])// ) {
        $self->{USER}{CHARNO} += length $&;
        return ( 'ringbond', int $1 );
    }

    # Polymer bonds. Must be requested via options.
    if( $self->{USER}{OPTIONS}{polymer} ) {
        if( $self->YYData->{INPUT} =~ s/^&([0-9])// ) {
            $self->{USER}{CHARNO} += length $&;
            return ( 'polybond', int $1 );
        }
    }

    my $char = substr( $self->YYData->{INPUT}, 0, 1 );
    if( $char ne '' ) {
        $self->YYData->{INPUT} = substr( $self->YYData->{INPUT}, 1 );
    }
    $self->{USER}{CHARNO} ++;
    return( $char, $char );
}

sub parse
{
    my( $self, $string, $options ) = @_;
    $options = {} unless $options;

    $self->YYData->{INPUT}   = $string;
    $self->{USER}{GRAPHS}    = [];
    $self->{USER}{POLYBONDS} = {};
    $self->{USER}{RINGBONDS} = {};
    $self->{USER}{ATOMNO}    = 0;
    $self->{USER}{CHARNO}    = 0;
    $self->{USER}{OPTIONS}   = $options;
    $self->YYParse( yylex   => \&_Lexer,
                    yyerror => \&_Error,
                    yydebug => $options->{debug} );

    if( scalar keys %{$self->{USER}{RINGBONDS}} ) {
        die "$0: unclosed ring bond(s) detected: " .
            join( ', ', sort { $a <=> $b } keys %{$self->{USER}{RINGBONDS}} ) .
            ".\n";
    }

    my @graphs = grep { defined } @{$self->{USER}{GRAPHS}};
    for my $graph (@graphs) {
        for my $atom (sort { $a->{number} <=> $b->{number} } $graph->vertices) {
            delete $atom->{graph};
            delete $atom->{index};
            if( !$options->{raw} ) {
                if( !exists $atom->{hcount} ) {
                    next if !exists $normal_valence{$atom->{symbol}};
                    my $degree = sum map { exists $bond_order{$_} ? $bond_order{$_} : 1 }
                                     map { $graph->has_edge_attribute( $atom, $_, 'bond' )
                                            ? $graph->get_edge_attribute( $atom, $_, 'bond' )
                                            : '-' }
                                         $graph->neighbours( $atom );
                    $degree = 0 unless $degree;
                    my( $valence ) = grep { $degree <= $_ }
                                          @{$normal_valence{$atom->{symbol}}};
                    next if !defined $valence;
                    $atom->{hcount} = $valence - $degree;
                }
                for (1..$atom->{hcount}) {
                    $graph->add_edge( $atom,
                                      { symbol => 'H',
                                        class  => 0,
                                        number => $self->{USER}{ATOMNO} } );
                    $self->{USER}{ATOMNO} ++;
                }
                delete $atom->{hcount};
            }
        }
    }

    return @graphs;
}

sub _add_bond
{
    my( $graph, $A, $B, $type ) = @_;

    if( $type ) {
        if( $graph->multiedged ) {
            # CAVEAT: This code does not seem to work correctly with
            # Graph releases prior to 0.9712.
            my $id = $graph->add_edge_get_id( $A, $B );
            $graph->set_edge_attribute_by_id( $A, $B, $id, 'bond', $type );
        } else {
            $graph->set_edge_attribute( $A, $B, 'bond', $type );
        }
    } else {
        $graph->add_edge( $A, $B );
    }
}

sub _add_ring_bond
{
    my( $self, $atom, $ring_bond, $bond, $options ) = @_;

    $options = {} unless $options;
    my( $storage_name, $allow_reuse ) =
        ( $options->{storage_name}, $options->{allow_reuse} );
    $storage_name = 'ring' unless $storage_name;

    my $bonds = $self->{USER}{uc $storage_name . 'BONDS'};

    if( $bonds->{$ring_bond} ) {
        $self->_merge_graphs( $bonds->{$ring_bond}{atom}{index},
                              $atom->{index} );

        if( $bond && $bonds->{$ring_bond}{bond} &&
            $bond ne $bonds->{$ring_bond}{bond} ) {
            die "$0: ring bond types for ring bond $ring_bond do not match.\n";
        }
        ( $bond ) = grep { defined }
                         ( $bond, $bonds->{$ring_bond}{bond} );
        if( !$bond && is_aromatic $bonds->{$ring_bond}{atom} &&
            is_aromatic $atom ) {
            $bond = ':';
        }
        _add_bond( $atom->{graph},
                   $bonds->{$ring_bond}{atom},
                   $atom,
                   $bond && $bond ne '-' ? $bond : undef );
        delete $bonds->{$ring_bond} unless $allow_reuse;
    } else {
        $bonds->{$ring_bond} =
            { atom => $atom, $bond ? ( bond => $bond ) : () };
    }
}

sub _add_poly_bond
{
    my( $self, $atom, $poly_bond, $bond ) = @_;
    return $self->_add_ring_bond( $atom,
                                  $poly_bond,
                                  $bond,
                                  { storage_name => 'poly',
                                    allow_reuse  => 1 } );
}

sub _merge_graphs
{
    my( $self, $index1, $index2 ) = @_;
    return if $index1 == $index2;

    my $g1 = $self->{USER}{GRAPHS}[$index1];
    my $g2 = $self->{USER}{GRAPHS}[$index2];

    for ($g2->vertices) {
        $_->{graph} = $g1;
        $_->{index} = $index1;
    }
    $g1->add_vertices( $g2->vertices );

    for ($g2->edges) {
        my  $attributes = $g2->get_edge_attributes( @$_ );
        if( $attributes ) {
            $g1->set_edge_attributes( @$_, $attributes );
        } else {
            $g1->add_edge( @$_ );
        }
    }

    $self->{USER}{GRAPHS}[$index2] = undef;
}

1;
